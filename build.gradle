buildscript {
	dependencies {
		classpath 'com.h2database:h2:1.4.197'
	}
}

plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'org.flywaydb.flyway' version '6.2.4'
}

flyway {
	url = 'jdbc:h2:file:./build/h2'
	user = 'sa'
}

group = 'hmo.passcheck'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', 'build/classes/java/main']
		}
	}
}

ext {
	lombokVersion = '1.18.10'
	mapStructVersion = '1.3.1.Final'
	springFoxVersion = '2.9.2'
	swaggerAnnotationsVersion = '1.5.21'
	ehcacheVersion = '3.8.1'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.flywaydb:flyway-core'
	implementation "io.springfox:springfox-swagger2:${springFoxVersion}"
	implementation "io.springfox:springfox-swagger-ui:${springFoxVersion}"
	implementation "org.ehcache:ehcache:${ehcacheVersion}"
	compileOnly "org.projectlombok:lombok:${lombokVersion}", "org.mapstruct:mapstruct:${mapStructVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}", "org.mapstruct:mapstruct-processor:${mapStructVersion}"

	// spring-boot-starter-test already include JUnit5, Mockito and AssertJ
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	runtimeOnly 'com.h2database:h2'
}

test {
	useJUnitPlatform()
}
